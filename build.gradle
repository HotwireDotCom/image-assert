buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.2.31"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.16"
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.17.0"
    id "maven"
    id "maven-publish"
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:1.2.31"
    compile 'com.google.code.gson:gson:2.8.2'
    testCompile 'junit:junit:4.12'
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from dokka.outputDirectory
}

publishing {
    publications {
        MavenLocal(MavenPublication) {
            from components.java
            artifactId 'image-assert'
            groupId 'com.hotwire.image-assert'
            version = '1.0.5'
            artifact(sourceJar) {
                classifier "sources"
            }
            artifact(javadocJar)
            pom.withXml() {
                def root = asNode()
                root.appendNode('packaging', 'jar')
                root.appendNode('name', 'Image assertion library')
                root.appendNode('description', 'Image assertion library brings image comparison into automated testing.')
                root.appendNode('url', 'https://github.com/HotwireDotCom/image-assert')
                def scm = root.appendNode('scm')
                scm.appendNode('url', 'https://github.com/HotwireDotCom/image-assert')
                scm.appendNode('connection', 'scm:https://github.com/HotwireDotCom/image-assert.git')
                scm.appendNode('developerConnection', 'scm:git://github.com/HotwireDotCom/image-assert.git')
                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The MIT License (MIT)')
                license.appendNode('url', 'http://www.opensource.org/licenses/MIT')
                license.appendNode('distribution', 'repo')
                def developer = root.appendNode('developers').appendNode('developer')
                developer.appendNode('name', 'Dmitry Berezhnoy')
                developer.appendNode('email', 'deadmoto@gmail.com')
            }
        }
    }
    repositories {
        maven {
            url "$buildDir/publishing"
        }
    }
}
